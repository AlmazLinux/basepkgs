pkgname=pacman
pkgver=6.0.0
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
backup=(etc/pacman.conf etc/makepkg.conf)
source=(${pkgname}-${pkgver}.tar.xz
	pacman-6.0.0-fix-404-download.patch
	pacman-6.0.0-fix-key-import-double-free.patch
        pacman.conf
        makepkg.conf)

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/pacman-6.0.0-fix-404-download.patch
  patch -p1 -i "$srcdir"/pacman-6.0.0-fix-key-import-double-free.patch
}

build() {
  cd "$pkgname-$pkgver"

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=disabled \
        -Ddoxygen=disabled \
        -Dscriptlet-shell=/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

package() {
  cd "$pkgname-$pkgver"
  
  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
