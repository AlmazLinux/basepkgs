pkgname=grub
pkgver=2.06
pkgrel=1
pkgdesc="GNU GRand Unified Bootloader (2)"
arch=('x86_64')
url="https://www.gnu.org/software/grub/"
license=('GPL3')
source=(${pkgname}-${pkgver}.tar.xz
	lang-C.UTF-8.patch
	fix-gcc-no-pie-specs.patch
	alpine-mkconfig.patch)

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i ${srcdir}/lang-C.UTF-8.patch
    patch -Np1 -i ${srcdir}/fix-gcc-no-pie-specs.patch
    patch -Np1 -i ${srcdir}/alpine-mkconfig.patch
    sed "s/gold-version/& -R .note.gnu.property/" \
    -i Makefile.in grub-core/Makefile.in
    PYTHON=python3 ./autogen.sh
}

build() {
    cd "${pkgname}-${pkgver}"
    grub_cv_check_uscore_uscore_bss_start_symbol=yes  \
         grub_cv_check_edata_symbol=yes  \
         grub_cv_check_uscore_edata_symbol=yes  \
         grub_cv_check_end_symbol=yes   \
         grub_cv_check_uscore_end_symbol=yes  \
         CC=clang CXX=clang++ LD=ld.bfd   \
         CFLAGS="-Wno-error --ld-path=/usr/bin/ld.bfd" \
         CXXFLAGS="--ld-path=/usr/bin/ld.bfd" \
         ./configure --prefix=/usr          \
	 --sbindir=/sbin        \
	 --sysconfdir=/etc      \
	 --disable-efiemu       \
	 --disable-werror
    sed -i 's/TARGET_IMG_LDFLAGS = -Wl,-N/TARGET_IMG_LDFLAGS = -Wl,-N,--no-pie/' Makefile
    sed -i 's/TARGET_LDFLAGS_OLDMAGIC = -Wl,-N/TARGET_LDFLAGS_OLDMAGIC = -Wl,-N,--no-pie/' Makefile
    sed -i 's/TARGET_IMG_LDFLAGS = -Wl,-N/TARGET_IMG_LDFLAGS = -Wl,-N,--no-pie/' grub-core/Makefile
    sed -i 's/TARGET_LDFLAGS_OLDMAGIC = -Wl,-N/TARGET_LDFLAGS_OLDMAGIC = -Wl,-N,--no-pie/' grub-core/Makefile

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
