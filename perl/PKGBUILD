pkgname=perl
pkgver=5.34.1
pkgrel=1
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
url="http://www.perl.org/"
license=('GPL' 'PerlArtistic')
source=(${pkgname}-${pkgver}.tar.xz
	musl-locale.patch
	musl-stack-size.patch)

prepare() {
    cd "${pkgname}-${pkgver}"

    patch -Np1 -i ${srcdir}/musl-locale.patch
    patch -Np1 -i ${srcdir}/musl-stack-size.patch
}

build() {
    cd "${pkgname}-${pkgver}"

    export BUILD_ZLIB=False
    export BUILD_BZIP2=0

    CC=clang CXX=clang++ \
    sh Configure -des -Dprefix=/usr                                \
                      -Dvendorprefix=/usr                          \
                      -Dprivlib=/usr/lib/perl5/5.34/core_perl      \
                      -Darchlib=/usr/lib/perl5/5.34/core_perl      \
                      -Dsitelib=/usr/lib/perl5/5.34/site_perl      \
                      -Dsitearch=/usr/lib/perl5/5.34/site_perl     \
                      -Dvendorlib=/usr/lib/perl5/5.34/vendor_perl  \
                      -Dvendorarch=/usr/lib/perl5/5.34/vendor_perl \
                      -Dman1dir=/usr/share/man/man1                \
                      -Dman3dir=/usr/share/man/man3                \
                      -Dpager="/usr/bin/less -isR"                 \
                      -Duseshrplib                                 \
                      -Dusethreads                                 \
                      -Dcccdlflags='-fPIC' -Dccdlflags='-rdynamic'
    make

    unset BUILD_ZLIB BUILD_BZIP2
}

check() {
    cd "${pkgname}-${pkgver}"
    make -k test 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"

    export BUILD_ZLIB=False
    export BUILD_BZIP2=0
    
    make DESTDIR=${pkgdir} install

    unset BUILD_ZLIB BUILD_BZIP2
}
