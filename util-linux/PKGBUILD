pkgname=util-linux
pkgver=2.36.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL2')
source=(${pkgname}-${pkgver}.tar.xz
	fix-musl.patch
	ttydefaults.h)

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np0 -i ${srcdir}/fix-musl.patch
    cp -v ${srcdir}/ttydefaults.h include/
    libtoolize.gnu --force && aclocal -I m4 && autoconf -v && automake --add-missing
    export CFLAGS="-D_DIRENT_HAVE_D_TYPE"
    NOCONFIGURE=1 ./autogen.sh
    unset CFLAGS
}

build() {
    cd "${pkgname}-${pkgver}"
    export CFLAGS="-D_DIRENT_HAVE_D_TYPE"
    CC=clang CXX=clang++    \
         LIBS="-lutmps -lskarnet" \
         ./configure ADJTIME_PATH=/var/lib/hwclock/adjtime   \
		--docdir=/usr/share/doc/util-linux-${pkgver} \
		--disable-chfn-chsh  \
		--disable-login      \
		--disable-nologin    \
		--disable-su         \
		--disable-setpriv    \
		--disable-runuser    \
		--disable-pylibmount \
		--disable-static     \
		--without-python     \
		--without-systemd    \
		--without-systemdsystemunitdir
    make
    unset CFLAGS
}

check() {
    cd "${pkgname}-${pkgver}"
    make -k check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"
    install -vdm755 ${pkgdir}/var/lib/hwclock
    make DESTDIR=${pkgdir} install
}
